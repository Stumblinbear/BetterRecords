buildscript {
    repositories {
        jcenter()
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

repositories {
    jcenter()
    maven {
        url 'http://maven.shadowfacts.net/'
    }
    maven {
        url "http://dl.bintray.com/jetbrains/spek"
    }
}

apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.github.johnrengelman.plugin-shadow'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

version = "$minecraft_version-$mod_version"

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = "$forge_mappings"
    runDir = "run"

    replaceIn "Constants.kt"
    replace "@VERSION@", project.version
}

dependencies {
    shadow group: 'net.shadowfacts', name: 'Forgelin', 'version': forgelin_version

    compile group: 'com.googlecode.soundlibs', name: 'vorbisspi', version: '1.0.3.3'
    compile group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5.4'

    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlin_version
    testCompile group: 'org.jetbrains.spek', name: 'spek-api', version: '1.1.5'
    testRuntime group: 'org.jetbrains.spek', name: 'spek-junit-platform-engine', version : '1.1.5'
}

shadowJar {
    classifier '' // Replace the default jar

    relocate 'org/tritonus', 'com/codingforcookies/betterrecords/repack/tritonus'
    relocate 'javazoom', 'com/codingforcookies/betterrecords/repack/javazoom'
    relocate 'com/jcraft', 'com/codingforcookies/betterrecords/repack/jcraft'
    relocate 'junit', 'com/codingforcookies/betterrecords/repack/junit' // Can't be bothered to figure out why so just move it
}

reobf {
    shadowJar {} // Reobf the shadowed jar
}

build.dependsOn shadowJar

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
}
